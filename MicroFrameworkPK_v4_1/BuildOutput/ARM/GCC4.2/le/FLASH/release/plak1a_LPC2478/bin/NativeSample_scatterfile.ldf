MEMORY
{
    LR_FLASH : ORIGIN = 0x80000000, LENGTH = 0x000B0000
    LR_IFLASH : ORIGIN = 0x00020000, LENGTH = 0x5D000
    LR_CONFIG : ORIGIN = 0x807D0000, LENGTH = 0x00010000
    HEAP : ORIGIN = 0xA0200000, LENGTH = 0xFFFF8 + 1
    STACK : ORIGIN = 0xA0300000, LENGTH = 0x100000 + 1
    RELOC_RAM : ORIGIN = 0xA0000000, LENGTH = 0xA0200000-0xA0000000
}
ENTRY(EntryPoint)
SECTIONS
{
    ER_FLASH  :
    {
        * (i.EntryPoint)
        * (SectionForBootstrapOperations)
        * (.text*)
        * (i.*)
        * (rodata)
        * (.rodata*)
        * (.constdata*)
        * (.conststring*)
        * (SectionForConfig)
        * (.glue*)
        PROVIDE(_sbrk = .);
        PROVIDE(_write = .);
        PROVIDE(_close = .);
        PROVIDE(_fstat = .);
        PROVIDE(_lseek = .);
        PROVIDE(_read = .);
        PROVIDE(_exit = .);
        PROVIDE(_getpid = .);
        PROVIDE(_kill = .);
        PROVIDE(abort = .);
        PROVIDE(__errno = .);
        PROVIDE(_read = .);
        PROVIDE(isatty = .);
        PROVIDE(_isatty = .);
        LONG(0xE12FFF1E); 
    }>LR_FLASH
    ER_RAM_RO  : ALIGN(0x10)
    {
        * (SectionForFlashOperations)
    }>RELOC_RAM AT>LR_FLASH
    ER_RAM_RW  : ALIGN(0x10)
    {
        * (rwdata)
        * (.data*)
    }>RELOC_RAM AT>LR_FLASH
    ER_IRAM 0x40000000 :
    {
        * (VectorsTrampolines)
    }AT>LR_FLASH
    ER_IRAM_TC 0x40000040 :
    {
        * (SectionForInternalSRAM)
    }AT>LR_FLASH
    .bss  : ALIGN(0x10)
    {
        * (.zidata*)
        * (.bss*)
        PROVIDE(__exidx_start = .); 
        PROVIDE(__exidx_end = .); 
    }>RELOC_RAM AT>LR_FLASH
    /DISCARD/  :
    {
        * (.ARM.exidx*)
        * (.ARM.extab*)
    }
    ER_HEAP_BEGIN 0xA0200000 :
    {
        * (SectionForHeapBegin)
    }
    ER_HEAP_END 0xA02FFFF8 :
    {
        * (SectionForHeapEnd)
    }
    ER_STACK_BOTTOM 0xA0300000 :
    {
        * (SectionForStackBottom)
    }
    ER_STACK_TOP 0xA0400000 :
    {
        * (SectionForStackTop)
    }
    ER_CONFIG  :
    {
        * (SectionForConfig)
    }>LR_CONFIG
}
Load$$ER_FLASH$$Base = ADDR(ER_FLASH);
Image$$ER_FLASH$$Length = SIZEOF(ER_FLASH);
Image$$ER_RAM_RO$$Base = ADDR(ER_RAM_RO);
Image$$ER_RAM_RO$$Length = SIZEOF(ER_RAM_RO);
Load$$ER_RAM_RO$$Base = LOADADDR(ER_RAM_RO);
Image$$ER_RAM_RW$$Base = ADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$Length = SIZEOF(ER_RAM_RW);
Load$$ER_RAM_RW$$Base = LOADADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$ZI$$Base = ADDR(.bss);
Image$$ER_RAM_RW$$ZI$$Length = SIZEOF(.bss);
__use_no_semihosting_swi = 0;
Image$$ER_IRAM$$Base = ADDR(ER_IRAM);
Image$$ER_IRAM$$Length = SIZEOF(ER_IRAM);
Load$$ER_IRAM$$Base = LOADADDR(ER_IRAM);
Image$$ER_IRAM_TC$$Base = ADDR(ER_IRAM_TC);
Image$$ER_IRAM_TC$$Length = SIZEOF(ER_IRAM_TC);
Load$$ER_IRAM_TC$$Base = LOADADDR(ER_IRAM_TC);
